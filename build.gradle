group 'org.giffardtechnologies'
version '1.1-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'java'

sourceCompatibility = 11

def homePath = System.properties['user.home']

def installDir = '/Users/ngm/Dev/API-Docs/restdocs'
def locPropertiesFile = project.rootProject.file('local.properties')
if (locPropertiesFile.exists()) {
    Properties locProperties = new Properties()
    locProperties.load(locPropertiesFile.newDataInputStream())
    installDir = locProperties.getProperty('installDir', installDir)
}

repositories {
    mavenCentral()
    ivy {
        // change to point to your repo, e.g. http://my.org/repo
        url = "${homePath}/.ivy/repo"
    }
    ivy {
        credentials {
            username "$allegoArtifactoryUser"
            password "$allegoArtifactoryPassword"
        }
        url "https://dev-build.allego.com/artifactory/$allegoAndroidArtifactoryRepo/"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile fileTree(includes: ['*.jar'], dir: 'manual-libs')
    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.23'
    // https://mvnrepository.com/artifact/org.apache.velocity/velocity
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    // https://mvnrepository.com/artifact/org.apache.velocity/velocity-tools
    compile group: 'org.apache.velocity', name: 'velocity-tools', version: '2.0'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    // https://github.com/remkop/picocli
    compile 'info.picocli:picocli:3.5.2'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.guava:guava:22.0'
    compile 'org.jetbrains:annotations:13.0'

    compile 'com.squareup:javapoet:1.13.0'
    compile 'com.google.googlejavaformat:google-java-format:1.8'
// https://mvnrepository.com/artifact/org.eclipse.jdt/org.eclipse.jdt.core
    compile group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.core', version: '3.22.0'

    compile "com.allego.common:futureproofenum:1.0-SNAPSHOT"

    def mapstruct_version = "1.3.1.Final"
    implementation "org.mapstruct:mapstruct:$mapstruct_version"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstruct_version"
}

mainClassName = 'com.giffardtechnologies.restdocs.DocGenerator'

startScripts {
    applicationName = 'doc_generator'
    def gen = unixStartScriptGenerator
    gen.template = resources.text.fromString(
            gen.template.asString().replaceFirst('(?=\nDEFAULT_JVM_OPTS=.*?\n)') {
                '\nJAVA_OPTS="\\$JAVA_OPTS "\'"-Dprogdir=\\$APP_HOME" "-Dexecdir=\\$SAVED"\''
            })
    def genWin = windowsStartScriptGenerator
    genWin.template = resources.text.fromString(
            genWin.template.asString().replaceFirst('(?=\nset DEFAULT_JVM_OPTS=.*?\n)') {
                '\nJAVA_OPTS="%JAVA_OPTS "\'"-Dprogdir=%APP_HOME" "-Dexecdir=%DIRNAME"\''
            })
}

task createJavaStartScript(type: Copy) {
    from file("$buildDir/scripts/doc_generator")
    rename { String filename -> return 'java_generator' }
    filter { String line -> line.replaceAll("(.*)DocGenerator(.*)", '$1JavaGenerator$2') }
    into file("$buildDir/scripts")
}
startScripts.finalizedBy createJavaStartScript

installDist {
    into installDir
}

task getDeps(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'runtime-libs/'
}